import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.2'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    group = 'net.siegmar.japt-proxy'

    sourceCompatibility = 1.7

    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }

    test {
        useTestNG()
    }

    checkstyleMain {
        configFile = new File(rootDir, "config/checkstyle/checkstyle.xml")
        configProperties = ['baseDir': "$rootDir"]

        classpath += configurations.compile
    }
    checkstyleTest {
        configFile = new File(rootDir, "config/checkstyle/checkstyle.xml")
        configProperties = ['baseDir': "$rootDir"]

        classpath += configurations.compile
    }
}

project(':app') {
    apply plugin: 'war'

    archivesBaseName = 'japt-proxy'

    dependencies {
        providedCompile('javax.servlet:javax.servlet-api:3.0.1')
        compile('org.apache.commons:commons-lang3:3.3.2')
        compile('commons-io:commons-io:2.4')
        compile('org.slf4j:slf4j-api:1.7.7')
        compile('org.slf4j:jcl-over-slf4j:1.7.7')
        //compile('org.slf4j:slf4j-log4j12:1.7.14')
        //compile 'log4j:log4j:1.2.17'
        compile('org.jdom:jdom2:2.0.5')
        compile('org.apache.httpcomponents:httpclient:4.3.3')
        compile('commons-net:commons-net:3.3')
        compile('ch.qos.logback:logback-classic:1.1.2')
        compile('commons-codec:commons-codec:1.9')
        compile('org.springframework:spring-web:4.0.5.RELEASE')
        testCompile('org.testng:testng:6.8.8')
        testCompile('org.springframework:spring-test:4.0.5.RELEASE')
    }

    processResources {
        filter ReplaceTokens, tokens: [ "application.version": version ]
    }
}

project(':standalone') {
    apply plugin: 'fatjar'

    archivesBaseName = 'japt-proxy-server'

    dependencies {
        compile project(':app')
        compile('org.mortbay.jetty:jetty:6.1.26')
    }

    fatJarPrepareFiles {
        include 'META-INF/spring.handlers'
        include 'META-INF/spring.schemas'
    }

    fatJar {
        manifest.attributes('Main-Class': 'net.siegmar.japtproxy.JaptProxyServer')
    }
}
